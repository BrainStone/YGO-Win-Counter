// Project-Type
apply plugin: "c"

model {
    components {
        lzma(NativeLibrarySpec) {
            if(System.properties['sun.arch.data.model'] == "64") {
                targetPlatform "x64"
            } else {
                targetPlatform "x86"
            }

            sources.c {
                source {
                    srcDirs "lzma-sdk-c"
                    include "Alloc.c"
                    include "LzFind.c"
                    include "LzFindMt.c"
                    include "LzmaDec.c"
                    include "LzmaEnc.c"
                    include "LzmaLib.c"
                    include "Threads.c"
                }
                exportedHeaders {
                    srcDirs "lzma-sdk-c"
                    include "LzmaLib.h"
                }
            }
        }
    }
    
    binaries {
        withType(SharedLibraryBinarySpec) {
            buildable = false
        }
        withType(StaticLibraryBinarySpec) {
            if (toolChain in Gcc) {
                cCompiler.define "_7ZIP_ST"
                cCompiler.args "-w", "-Ofast", "-g0"
            }
            if (toolChain in Clang) {
                cCompiler.define "_7ZIP_ST"
                cCompiler.args "-w", "-Ofast", "-g0"
            }
            if (toolChain in VisualCpp) {
                cCompiler.args "/W0", "/EHsc", "/O2"
            }
        }
    }
}
